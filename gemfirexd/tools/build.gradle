/*
 * Copyright (c) 2018 SnappyData, Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you
 * may not use this file except in compliance with the License. You
 * may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied. See the License for the specific language governing
 * permissions and limitations under the License. See accompanying
 * LICENSE file.
 */

plugins {
  id 'ca.coglinc.javacc' version '2.4.0'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}

artifacts {
  archives packageTests
}

dependencies {
  compileOnly project(subprojectBase + 'snappydata-store-core')
  compile project(subprojectBase + 'snappydata-store-hibernate')

  // ddlutils dependencies
  compile 'commons-beanutils:commons-beanutils:1.9.3'
  compile 'commons-codec:commons-codec:1.10'
  compile 'commons-configuration:commons-configuration:1.10'
  compile 'commons-pool:commons-pool:1.6'
  compile 'commons-dbcp:commons-dbcp:1.4'
  compile "jline:jline:${jlineVersion}"
  compile("org.apache.thrift:libthrift:${thriftVersion}") {
    exclude(group: 'org.slf4j', module: 'slf4j-api')
  }
  compile "com.esotericsoftware:kryo-shaded:${kryoVersion}"

  // use dunit support from snappydata
  if (subprojectBase == ':') {
    testCompile 'io.snappydata:dunit:${snappyDunitVersion}'
  } else {
    testCompile project(':dunit')
  }

  testCompile project(subprojectBase + 'snappydata-store-client')
  testCompile project(subprojectBase + 'gemfire-junit')

  compileOnly files("${System.getProperty('java.home')}/../lib/tools.jar")
  compileOnly 'xalan:xalan:2.7.2'
  compileOnly 'xalan:serializer:2.7.2'
  compileOnly "com.pivotal:pxf-api:${pxfVersion}"

  compileOnly "org.apache.hadoop:hadoop-annotations:${hadoopVersion}"
  compileOnly "org.apache.hadoop:hadoop-auth:${hadoopVersion}"
  compileOnly "org.apache.hadoop:hadoop-common:${hadoopVersion}"
  compileOnly "org.apache.hadoop:hadoop-hdfs:${hadoopVersion}"
  compileOnly "org.apache.hadoop:hadoop-mapreduce-client-core:${hadoopVersion}"
  compileOnly "com.google.protobuf:protobuf-java:${protobufVersion}"
  compileOnly "org.glassfish.jersey.core:jersey-server:${jerseyVersion}"
  compileOnly "org.glassfish.jersey.containers:jersey-container-servlet-core:${jerseyVersion}"
  compileOnly "org.eclipse.jetty:jetty-http:${jettyVersion}"
  compileOnly "org.eclipse.jetty:jetty-io:${jettyVersion}"
  compileOnly "org.eclipse.jetty:jetty-security:${jettyVersion}"
  compileOnly "org.eclipse.jetty:jetty-server:${jettyVersion}"
  compileOnly "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
  compileOnly "org.eclipse.jetty:jetty-util:${jettyVersion}"
  compileOnly "org.eclipse.jetty:jetty-webapp:${jettyVersion}"
  compileOnly "org.eclipse.jetty:jetty-xml:${jettyVersion}"
  compileOnly "com.google.code.findbugs:jsr305:${jsr305Version}"

  testCompile "org.apache.hbase:hbase-common:${hbaseVersion}"
  testCompile "org.apache.hbase:hbase-protocol:${hbaseVersion}"
  testCompile "org.apache.hbase:hbase-client:${hbaseVersion}"
  testCompile "org.apache.hbase:hbase-server:${hbaseVersion}"
  testCompile "com.sun.jersey:jersey-core:${sunJerseyVersion}"
  testCompile "com.sun.jersey:jersey-server:${sunJerseyVersion}"
  testCompile "com.sun.jersey:jersey-servlet:${sunJerseyVersion}"
  testCompile "org.mortbay.jetty:jetty:${hadoopJettyVersion}"
  testCompile "org.mortbay.jetty:jetty-util:${hadoopJettyVersion}"
  testCompile 'org.cloudera.htrace:htrace-core:2.05'
  testCompile "org.apache.hadoop:hadoop-common:${hadoopVersion}:tests"
  testCompile "org.apache.hadoop:hadoop-hdfs:${hadoopVersion}:tests"

  testCompile "com.pivotal:pxf-api:${pxfVersion}"
  testCompile "org.apache.ant:ant-launcher:${antVersion}"
  testCompile "org.apache.derby:derby:${derbyVersion}"
  testCompile "org.apache.derby:derbynet:${derbyVersion}"
  testRuntime "org.apache.derby:derbytools:${derbyVersion}"
  testRuntime "org.apache.derby:derbyclient:${derbyVersion}"
  testCompile 'oro:oro:2.0.8'
  testCompile 'dom4j:dom4j:1.6.1'
  // Embedded Apache Directory Server for LDAP tests
  testCompile(group: 'org.apache.directory.server', name: 'apacheds-all', version: '1.5.7') {
    exclude(group: 'org.apache.directory.shared', module: 'shared-ldap-schema')
  }

  testRuntime project(subprojectBase + 'snappydata-store-core')
}

// move javacc output directory to a place where IDEA can easily register
def javaccOut = 'src/generated/javacc'
compileJavacc {
  arguments = [ grammar_encoding: 'UTF-8' ]
  outputDirectory = file("${projectDir}/${javaccOut}")
}

task deleteGenerated(type: Delete) {
  delete javaccOut
}
clean.dependsOn deleteGenerated

sourceSets.main.java.srcDirs = [ 'src/main/java', javaccOut, 'src/ddlutils/java' ]
sourceSets.test.java.srcDirs = [ 'src/test/java', 'src/dunit/java', 'src/demo/java' ]

task generatePropertiesFiles(dependsOn: 'processResources') {
  def infoDir = file("${buildDir}/classes/main/com/pivotal/gemfirexd/internal/info")
  outputs.file "${infoDir}/tools.properties"
  inputs.dir "${projectDir}/../core/src/main/resources/com/pivotal/gemfirexd/internal/info"

  doLast {
    println('Creating tools properties')
    derbyProps.put('gemfirexd.product.technology.name', "${DERBY_NAME} Tools")
    writeProperties(infoDir, 'tools.properties', COPYRIGHT, derbyProps)
  }
}
compileJava.dependsOn generatePropertiesFiles

archivesBaseName = 'snappydata-store-tools'

shadowJar {
  dependsOn jar
  baseName = 'snappydata-tools'
  classifier = ''

  mergeServiceFiles()
}
